<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.0 on Fri Jan 16 23:51:00 2009 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>ajs_pixel_plot.pro (Anthony Smith's IDL routines)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="ajs_pixel_plot.pro (Anthony Smith's IDL routines)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; docformat = 'rst'</span>
<span class="comments">;+</span>
<span class="comments">; Plot (large number of) points as pixels (rectangles), with colour</span>
<span class="comments">; (white->black) representing density </span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;    x : in, required</span>
<span class="comments">;       x-coordinate of points</span>
<span class="comments">;    y : in, required</span>
<span class="comments">;       y-coordinate of points</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;    binsize1 : in, optional</span>
<span class="comments">;       Size of bin in x-direction</span>
<span class="comments">;    binsize2 : in, optional</span>
<span class="comments">;       Size of bin in y-direction</span>
<span class="comments">;    nbins1 : in, optional</span>
<span class="comments">;       Number of bins in x-direction (ignored if xrange and binsize1</span>
<span class="comments">;       set; default 50)</span>
<span class="comments">;    nbins2 : in, optional</span>
<span class="comments">;       Number of bins in y-direction (ignored if yrange and binsize2</span>
<span class="comments">;       set; default 50)</span>
<span class="comments">;    xrange : in, optional</span>
<span class="comments">;       Range for plot (if not overplot) and range for histogram</span>
<span class="comments">;    yrange : in, optional</span>
<span class="comments">;       Range for plot (if not overplot) and range for histogram</span>
<span class="comments">;    overplot : in, optional</span>
<span class="comments">;       Set /overplot to overplot</span>
<span class="comments">;    ct : in, optional</span>
<span class="comments">;       Colour table: -1 = use current, -2 = fixed colour (color keyword),</span>
<span class="comments">;       -3 (or not specified) = default (white -> black)</span>
<span class="comments">;       If color keyword set, and ct != -2, generate colour table from color</span>
<span class="comments">;    color : in, optional</span>
<span class="comments">;       Colour to use instead of grayscale/black</span>
<span class="comments">;    _REF_EXTRA : in, optional</span>
<span class="comments">;       Extra keywords for plot</span>
<span class="comments">; :Examples:</span>
<span class="comments">;    ajs_plot_radec, ra, dec, /nodata, xtitle='RA / degrees',</span>
<span class="comments">;    ytitle='dec / degrees'</span>
<span class="comments">;</span>
<span class="comments">;    ajs_pixel_plot, ra, sin(dec / !RADEG), nbins1=200, nbins2=200,</span>
<span class="comments">;    /overplot, ct=40</span>
<span class="comments">;</span>
<span class="comments">;    Sample output (click to enlarge):</span>
<span class="comments">;</span>
<span class="comments">;    &lt;a href="http://astronomy.sussex.ac.uk/~anthonys/images/sdss_radec.png">&lt;img src="http://astronomy.sussex.ac.uk/~anthonys/images/sdss_radec.png" width="200">&lt;/a></span>
<span class="comments">; :History:</span>
<span class="comments">;    18 Apr 2008 Written, Anthony Smith</span>
<span class="comments">;-</span>
PRO ajs_pixel_plot, x, y, binsize1=binsize1, binsize2=binsize2, $
                    nbins1=nbins1, nbins2=nbins2, $
                    xrange=xrange, yrange=yrange, overplot=overplot, $
                    ct=ct, color=color, _REF_EXTRA=e
  compile_opt idl2

  <span class="comments">;; No nbins? (Redundant if binsizes are set)</span>
  IF n_elements(nbins1) EQ 0 THEN $
     nbins1 = 50
  IF n_elements(nbins2) EQ 0 THEN $
     nbins2 = 50

  <span class="comments">;; Overplot: get xrange & yrange from plot?</span>
  IF keyword_set(overplot) THEN BEGIN
      IF n_elements(xrange) EQ 0 THEN $
         xrange = !x.crange
      IF n_elements(yrange) EQ 0 THEN $
         yrange = !y.crange
  ENDIF 

  <span class="comments">;; No bin sizes?</span>
  IF n_elements(binsize1) EQ 0 THEN $
     IF n_elements(xrange) EQ 0 THEN $ 
        binsize1 = (max(x) - min(x)) / float(nbins1) $
     ELSE $
        binsize1 = (max(xrange) - min(xrange)) / float(nbins1)
  IF n_elements(binsize2) EQ 0 THEN $
     IF n_elements(yrange) EQ 0 THEN $ 
        binsize2 = (max(y) - min(y)) / float(nbins2) $
     ELSE $
        binsize2 = (max(yrange) - min(yrange)) / float(nbins2)

  <span class="comments">;; Range for plot (if not set)</span>
  IF n_elements(xrange) EQ 0 THEN $ 
     xrange = [min(x), $
               (fix((max(x) - min(x)) / binsize1) + 1) * binsize1 + min(x)]
  IF n_elements(yrange) EQ 0 THEN $
     yrange = [min(y), $
               (fix((max(y) - min(y)) / binsize2) + 1) * binsize2 + min(y)]

  <span class="comments">;; Make histogram</span>
  min = [min(xrange), min(yrange)]
  max = [max(xrange), max(yrange)]
  hist = hist_nd([1#x, 1#y], [binsize1, binsize2], $
                 min=min, max=max)
  dims = size(hist, /dimensions)

  <span class="comments">;; Edges of the bins (left/bottom)</span>
  obin1 = dindgen(dims[0]) * binsize1 + min[0]
  obin2 = dindgen(dims[1]) * binsize2 + min[1]

  <span class="comments">;; Convert obin1 and obin2 to array with same dimensions as hist</span>
  xbin = rebin(obin1, n_elements(obin1), n_elements(obin2))
  ybin = rebin(transpose(obin2), n_elements(obin1), n_elements(obin2))

  <span class="comments">;; Axes</span>
  IF ~ keyword_set(overplot) THEN $ 
     plot, [0], xrange=xrange, yrange=yrange, _STRICT_EXTRA=e, /nodata

  <span class="comments">;; Plot pixels</span>
  boxx = [0, 1, 1, 0, 0] * binsize1 * 1.05 <span class="comments">; No gaps between pixels</span>
  boxy = [0, 0, 1, 1, 0] * binsize2 * 1.05

  nonzero = where(hist GT 0, n_nonzero)  
  IF n_nonzero GT 0 THEN BEGIN
      <span class="comments">;; Get current colour table (restore later)</span>
      tvlct, r_orig, g_orig, b_orig, /get

      IF n_elements(ct) EQ 0 THEN $
         ct = -3

      IF n_elements(color) GT 0 AND ct NE -2 THEN BEGIN
          <span class="comments">;; Generate colour table </span>
          <span class="comments">;; From white (0) to the input colour (255)</span>
          color_convert, r_orig, g_orig, b_orig, $
                         h_orig, l_orig, s_orig, /rgb_hls
          hls = double([h_orig[color], l_orig[color], s_orig[color]])
          h_curr = replicate(hls[0], 256)
          l_curr = 1. - findgen(256) * (1. - hls[1]) / 255
          s_curr = replicate(hls[2], 256)
          tvlct, h_curr, l_curr, s_curr, /hls
          colors = (hist[nonzero] * 255. / max(hist))
      ENDIF ELSE IF ct EQ -2 THEN BEGIN
          <span class="comments">;; Fixed colour</span>
          IF n_elements(color) GT 0 THEN $ 
             colors = replicate(color, n_nonzero) $
          ELSE $
             colors = replicate(0, n_nonzero)
      ENDIF ELSE IF ct GE 0 THEN BEGIN 
          <span class="comments">;; Load colour table</span>
          loadct, ct
          colors = (hist[nonzero] * 255. / max(hist))          
      ENDIF ELSE IF ct EQ -3 THEN BEGIN 
          <span class="comments">;; Default behaviour (white -> black)</span>
          loadct, 0
          colors = 255 - (hist[nonzero] * 255. / max(hist))
      ENDIF ELSE IF ct EQ -1 THEN BEGIN
          <span class="comments">;; Use current colour table</span>
          colors = (hist[nonzero] * 255. / max(hist))              
      ENDIF ELSE $
         message, 'Don''t know which colours to use!'

      <span class="comments">;; Plot</span>
      FOR i = 0, n_nonzero - 1 DO BEGIN
          polyfill, xbin[nonzero[i]] + boxx, ybin[nonzero[i]] + boxy, $
                    color=colors[i]
      ENDFOR 

      <span class="comments">;; Restore colour table</span>
      tvlct, r_orig, g_orig, b_orig
  ENDIF

END
</code>
    </div>
  </body>
</html>