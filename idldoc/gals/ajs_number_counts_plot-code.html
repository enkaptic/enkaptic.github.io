<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.0 on Fri Jan 16 23:50:55 2009 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>ajs_number_counts_plot.pro (Anthony Smith's IDL routines)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="ajs_number_counts_plot.pro (Anthony Smith's IDL routines)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; docformat = 'rst'</span>
<span class="comments">;+</span>
<span class="comments">; This procedure produces a plot of the number counts.</span>
<span class="comments">;-</span>


<span class="comments">;+</span>
<span class="comments">; Choose and set xrange</span>
<span class="comments">;-</span>
FUNCTION ajs_number_counts_plot_xrange, $
   bincentres=bincentres, logflux=logflux
  compile_opt idl2, hidden

  <span class="comments">;; Set xrange limits</span>
  IF n_elements(bincentres) GT 0 THEN BEGIN
      <span class="comments">;; ... from data points</span>
      binwidth = (max(bincentres) - min(bincentres)) $
                 / (n_elements(bincentres) - 1)
      xrange = [min(bincentres) - binwidth / 2., $
                max(bincentres) + binwidth / 2.]
  ENDIF ELSE $
     message, 'Cannot determine xrange'

  return, xrange
END


<span class="comments">;+</span>
<span class="comments">; Choose and set yrange</span>
<span class="comments">;-</span>
FUNCTION ajs_number_counts_plot_yrange, $
   ngals=ngals, input_logngals=input_logngals, $
   plot_logngals=plot_logngals, err_ngals=err_ngals, $
   pos_err_ngals=pos_err_ngals, neg_err_ngals=neg_err_ngals
  compile_opt idl2, hidden

  <span class="comments">;; Set yrange limits</span>
  <span class="comments">;; Todo: include error bars</span>
  IF n_elements(ngals) GT 0 THEN BEGIN
      <span class="comments">;; ... from data points</span>
      IF keyword_set(input_logngals) THEN $
         yrange = 10. ^ [min(ngals[where(finite(ngals))]) - 10, $
                         max(ngals[where(finite(ngals))]) + 2] $
      ELSE $
         yrange = [min(ngals[where(finite(ngals))]) / 10, $
                   max(ngals[where(finite(ngals))]) * 2]
  ENDIF ELSE $
     message, 'Cannot determine yrange'

  <span class="comments">;; Y-axis log(ngals) rather than ngals?</span>
  IF keyword_set(plot_logngals) THEN $
     yrange = alog10(yrange)

  return, yrange
END


<span class="comments">;+</span>
<span class="comments">; Add legend text to the plot</span>
<span class="comments">;-</span>
PRO ajs_number_counts_plot_legend, $
   legend_text=legend_text, legend_pos=legend_pos, $
   arg_legend_pos=arg_legend_pos, $
   color=color, psym=psym, _REF_EXTRA=e
  compile_opt idl2, hidden
  debug = ajs_debug()

  <span class="comments">;; Create legend text?</span>
  IF n_elements(legend_text) GT 0 THEN BEGIN 
      IF n_elements(legend_pos) LE 1 THEN BEGIN
          xpos = 0.1
          ypos = 0.9
      ENDIF ELSE BEGIN
          xpos = legend_pos[0]
          ypos = legend_pos[1]
      ENDELSE 

      <span class="comments">;; Symbol/line (to left on plot = + if x-axis reversed)</span>
      IF n_elements(psym) GT 0 THEN $
         oplot, [ajs_plot_pos(xpos - 0.04)], $
                [ajs_plot_pos(y_in=ypos + 0.01)], psym=psym, color=color
      IF psym LE 0 THEN $
            oplot, ajs_plot_pos([xpos - 0.06, xpos - 0.02]), $
                   ajs_plot_pos(y_in=[ypos + 0.01, ypos + 0.01]), $
                   color=color, _STRICT_EXTRA=e
      
      <span class="comments">;; Text</span>
      IF debug GE 2 THEN $
         message, 'xpos, ypos = ' + string(xpos) + string(ypos), /inf
      xyouts, ajs_plot_pos(xpos), ajs_plot_pos(y_in=ypos), legend_text, $
              color=color
      
      <span class="comments">;; Position for next legend item</span>
      IF arg_legend_pos THEN BEGIN
          legend_pos = [xpos, ypos - 0.05]
      ENDIF ELSE BEGIN
          message, 'Supply variable name for legend_pos for new values', /inf
      ENDELSE          
  ENDIF
END


<span class="comments">;+</span>
<span class="comments">; This procedure produces a plot of the number counts.</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;    bincentres : in, optional</span>
<span class="comments">;       x-position (e.g., magnitude, flux, log-flux)</span>
<span class="comments">;    ngals : in, optional</span>
<span class="comments">;       Number density, per square degree, per unit (x_unit)</span>
<span class="comments">;    err_ngals : in, optional</span>
<span class="comments">;       Uncertainty in ngals</span>
<span class="comments">;    neg_err_ngals : in, optional</span>
<span class="comments">;       Negative error in ngals, for asymmetric</span>
<span class="comments">;    pos_err_ngals : in, optional</span>
<span class="comments">;       Positive error in ngals, for asymmetric</span>
<span class="comments">;    mag : in, optional</span>
<span class="comments">;       Array of magnitudes (or log(flux) with /logflux)</span>
<span class="comments">;    area : in, optional</span>
<span class="comments">;       Area in square degrees (for use with mag)</span>
<span class="comments">;    nbins : in, optional</span>
<span class="comments">;       Number of bins for mag (use with xrange or bincentres)</span>
<span class="comments">;    overplot : in, optional</span>
<span class="comments">;       Set /overplot to overplot</span>
<span class="comments">;    plotfile : in, optional</span>
<span class="comments">;       EPS file name</span>
<span class="comments">;    show_plot : in, optional</span>
<span class="comments">;       Set /show_plot to open the EPS file for viewing</span>
<span class="comments">;    open : in, optional</span>
<span class="comments">;       Set /open to open the EPS file and leave it for overplotting</span>
<span class="comments">;    close : in, optional</span>
<span class="comments">;       Set /close to close the EPS plotfile</span>
<span class="comments">;    xrange : in, out, optional</span>
<span class="comments">;    yrange : in, out, optional</span>
<span class="comments">;    input_logngals : in, optional</span>
<span class="comments">;       Set /input_logngals to indicate data values are log(n) rather</span>
<span class="comments">;       than n</span>
<span class="comments">;    plot_logngals : in, optional</span>
<span class="comments">;       Set /plot_logngals to plot log(n) rather than n (redundant</span>
<span class="comments">;       if overplotting)</span>
<span class="comments">;    logflux : in, optional</span>
<span class="comments">;       Set /logflux for log flux rather than magnitudes</span>
<span class="comments">;    ylog : in, optional, default=1-plot_logn</span>
<span class="comments">;    psym : in, optional</span>
<span class="comments">;       Uses D. Fanning's symcat (e.g., 16 = filled circle) for</span>
<span class="comments">;       non-standard psym values</span>
<span class="comments">;    symcat_thick : in, optional</span>
<span class="comments">;       Thick keyword for symcat</span>
<span class="comments">;    color : in, optional</span>
<span class="comments">;    xstyle : in, optional</span>
<span class="comments">;       Default xstyle=1</span>
<span class="comments">;    ystyle : in, optional</span>
<span class="comments">;       Default ystyle=1</span>
<span class="comments">;    title : in, optional</span>
<span class="comments">;       Title for plot</span>
<span class="comments">;    xtitle : in, optional</span>
<span class="comments">;       Title for x-axis</span>
<span class="comments">;    ytitle : in, optional</span>
<span class="comments">;       Title for y-axis</span>
<span class="comments">;    legend_pos : in, out, optional, type=fltarr(2)</span>
<span class="comments">;       [xpos, ypos] for legend text. Output is position for next</span>
<span class="comments">;       legend label, for overplotting. varies from [0, 0] = [left,</span>
<span class="comments">;       bottom] to [1, 1] = [right, top].</span>
<span class="comments">;    legend_text : in, optional</span>
<span class="comments">;       Legend text</span>
<span class="comments">;    euclid : in, optional</span>
<span class="comments">;       Set /euclid to subtract Euclidean slope, or euclid=x to then</span>
<span class="comments">;       divide by x (specify axis ranges and titles explicitly)</span>
<span class="comments">;    _REF_EXTRA : in, optional</span>
<span class="comments">;       Extra keywords for plots</span>
<span class="comments">;</span>
<span class="comments">; :Examples:</span>
<span class="comments">;    bincentres = [10, 11, 12]</span>
<span class="comments">;</span>
<span class="comments">;    ngals = [10, 100, 1000]</span>
<span class="comments">;</span>
<span class="comments">;    err_ngals = [1, 10, 100]</span>
<span class="comments">;</span>
<span class="comments">;    ajs_number_counts_plot, bincentres=bincentres, ngals=ngals,</span>
<span class="comments">;    err_ngals=err_ngals</span>
<span class="comments">;</span>
<span class="comments">; :History:</span>
<span class="comments">;    29 Oct 2008 Created, Anthony Smith</span>
<span class="comments">;</span>
<span class="comments">;    16 Jan 2009 Added euclid keyword, AJS</span>
<span class="comments">;-</span>
PRO ajs_number_counts_plot, $
   bincentres=bincentres, $
   ngals=ngals, $
   err_ngals=err_ngals, $
   neg_err_ngals=neg_err_ngals, $
   pos_err_ngals=pos_err_ngals, $
   mag=mag, $
   area=area, $
   nbins=nbins, $
   overplot=overplot, $
   plotfile=plotfile, $
   show_plot=show_plot, $
   open=open, $
   close=close, $
   xrange=xrange, $
   yrange=yrange, $
   input_logngals=input_logngals, $
   plot_logngals=plot_logngals, $
   logflux=logflux, $
   ylog=ylog, $
   psym=psym, $
   symcat_thick=symcat_thick, $
   color=color, $
   xstyle=xstyle, $
   ystyle=ystyle, $
   title=title, $
   xtitle=xtitle, $
   ytitle=ytitle, $
   legend_pos=legend_pos, $
   legend_text=legend_text, $
   euclid=euclid, $
   _REF_EXTRA=e

  compile_opt idl2
  debug = ajs_debug()
  IF debug GE 1 THEN BEGIN 
      message, 'Plotting number counts', /inf
      message, ajs_kw_string(overplot=overplot, $
                             plotfile=plotfile, show_plot=show_plot, $
                             open=open, close=close), /inf
  ENDIF 

  <span class="comments">;; Keywords</span>
  <span class="comments">;; Won't go wrong if symcat not available</span>
  IF n_elements(psym) GT 0 THEN BEGIN
      IF abs(psym) EQ 9 OR abs(psym) GT 10 THEN $
         psym_use = symcat(abs(psym), thick=symcat_thick) $
      ELSE $
         psym_use = psym
      psym_use = abs(psym_use) * (psym GT 0 ? 1 : -1)
  ENDIF ELSE $
     psym_use = 0
     
  <span class="comments">;; Plot something?</span>
  IF NOT keyword_set(close) THEN BEGIN
      <span class="comments">;; Any data to plot?</span>
      IF n_elements(ngals) EQ 0 AND n_elements(mag) EQ 0 $ 
         AND NOT keyword_set(open) EQ 0 THEN $
            message, 'No data!'

      <span class="comments">;; Setup plot</span>
      eps = keyword_set(open) OR keyword_set(show_plot)
      ajs_plot_start, plotfile=plotfile, eps=eps


      <span class="comments">;; Choose source of phi data: (1) ngals or (2) mag</span>
      IF n_elements(ngals) EQ 0 THEN BEGIN
          IF n_elements(mag) GT 0 THEN BEGIN
              <span class="comments">;; Data from magnitudes (or log fluxes)</span>
              <span class="comments">;; Calculate number counts</span>
              IF debug GE 1 THEN message, 'Calculating from mags', /inf
              ngals = ajs_number_counts(mag, area=area, $
                                        bincentres=bincentres, $
                                        err_ngals=err_ngals, nbins=nbins, $
                                        xrange=xrange)
              input_logngals = 0
          ENDIF
      ENDIF ELSE $
         IF debug GE 1 THEN message, 'Using supplied values of phi', /inf

      <span class="comments">;; Create axes (if not overplot)</span>
      IF NOT keyword_set(overplot) THEN BEGIN
          <span class="comments">;; Titles not set</span>
          IF n_elements(ytitle) EQ 0 THEN BEGIN
              IF keyword_set(logflux) THEN BEGIN
                  IF keyword_set(plot_logngals) THEN $
                     ytitle = 'log (N / (dex!U-1!N deg!U-2!N) )' $
                  ELSE $
                     ytitle = 'N / (dex!U-1!N deg!U-2!N)'
              ENDIF ELSE BEGIN
                  IF keyword_set(plot_logngals) THEN $
                     ytitle = 'log (N / (mag!U-1!N deg!U-2!N) )' $
                  ELSE $
                     ytitle = 'N / (mag!U-1!N deg!U-2!N)'
              ENDELSE
          ENDIF
          IF n_elements(xtitle) EQ 0 THEN BEGIN
              IF keyword_set(logflux) THEN BEGIN
                  xtitle = 'log flux'
              ENDIF ELSE BEGIN
                  xtitle = 'magnitude'
              ENDELSE
          ENDIF

          <span class="comments">;; No xrange/yrange set</span>
          IF n_elements(xrange) LE 1 THEN $
             xrange = ajs_number_counts_plot_xrange(bincentres=bincentres, $
                                                    logflux=logflux)
          IF n_elements(yrange) LE 1 THEN $
             yrange = ajs_number_counts_plot_yrange( $
                      ngals=ngals, input_logngals=input_logngals, $
                      plot_logngals=plot_logngals, err_ngals=err_ngals, $
                      pos_err_ngals=pos_err_ngals, neg_err_ngals=neg_err_ngals)

          <span class="comments">;; ylog if not plot_logn</span>
          IF n_elements(ylog) EQ 0 AND NOT keyword_set(plot_logngals) THEN $
             ylog = 1

          <span class="comments">;; Set xstyle & ystyle = 1 by default</span>
          IF n_elements(xstyle) EQ 0 THEN $
             xstyle=1
          IF n_elements(ystyle) EQ 0 THEN $
             ystyle=1

          <span class="comments">;; Plot empty axes</span>
          plot,[0],[1], xrange=xrange, yrange=yrange, /nodata, $
               title=title, ytitle=ytitle, xtitle=xtitle, ylog=ylog, $
               xstyle=xstyle, ystyle=ystyle, _STRICT_EXTRA=e
      ENDIF
      plot_logngals = 1 - !y.type

      <span class="comments">;; Define filled circle (IDL Help): psym=8</span>
      <span class="comments">;; Obselete: now uses D. Fanning's symcat routine</span>
<span class="comments">;;       A = FINDGEN(17) * (!PI * 2 / 16.)  </span>
<span class="comments">;;       USERSYM, COS(A), SIN(A), /FILL  </span>

      <span class="comments">;; Plot data points?</span>
      IF n_elements(ngals) GT 0 THEN BEGIN 
          <span class="comments">;; Error bars</span>
          IF n_elements(err_ngals) GT 0 THEN BEGIN
              neg_err_ngals = err_ngals
              pos_err_ngals = err_ngals
          ENDIF
          IF n_elements(neg_err_ngals) GT 0 THEN BEGIN 
              ngals_err_max = ngals + pos_err_ngals
              ngals_err_min = ngals - neg_err_ngals
              IF NOT keyword_set(input_logngals) THEN BEGIN 
                  <span class="comments">;; Infinite plot range?</span>
                  too_small = where(ngals_err_min LE 0)
                  IF too_small[0] NE -1 THEN $
                     ngals_err_min[too_small] = 1e-30 <span class="comments">; log(ngals) > 0</span>
              ENDIF
              IF keyword_set(input_logngals) $
                 AND NOT keyword_set(plot_logngals) THEN BEGIN
                  ngals_err_max = 10. ^ ngals_err_max
                  ngals_err_min = 10. ^ ngals_err_min                 
              ENDIF ELSE IF NOT keyword_set(input_logngals) $
                 AND keyword_set(plot_logngals) THEN BEGIN
                  ngals_err_max = alog10(ngals_err_max)
                  ngals_err_min = alog10(ngals_err_min)
              ENDIF
          ENDIF

          <span class="comments">;; log(ngals) ? (NB don't change ngals itself!)</span>
          IF keyword_set(input_logngals) $
             AND NOT keyword_set(plot_logngals) THEN $
                ngals_plot = 10. ^ ngals $
          ELSE IF NOT keyword_set(input_logngals) $
             AND keyword_set(plot_logngals) THEN $
                ngals_plot = alog10(ngals) $
          ELSE $
             ngals_plot = ngals

          <span class="comments">;; Plot the data</span>
          IF n_elements(neg_err_ngals) GT 0 THEN BEGIN
              IF n_elements(euclid) GT 0 THEN BEGIN
                  <span class="comments">;; With error bars</span>
                  oploterror, bincentres, $
                              ngals_plot / 10 ^ (0.6 * bincentres) / euclid, $
                              (ngals_plot - ngals_err_min) $
                               / 10 ^ (0.6 * bincentres) / euclid, $
                              /lobar, psym=psym_use, $
                              color=color, errcolor=color, _STRICT_EXTRA=e
                  oploterror, bincentres, $
                              ngals_plot / 10 ^ (0.6 * bincentres) / euclid, $
                              (ngals_err_max - ngals_plot) $
                               / 10 ^ (0.6 * bincentres) / euclid, $
                              /hibar, psym=psym_use, $
                              color=color, errcolor=color, _STRICT_EXTRA=e
                  
              ENDIF ELSE BEGIN
                  <span class="comments">;; With error bars</span>
                  oploterror, bincentres, ngals_plot, $
                              ngals_plot - ngals_err_min, $
                              /lobar, psym=psym_use, $
                              color=color, errcolor=color, _STRICT_EXTRA=e
                  oploterror, bincentres, ngals_plot, $
                              ngals_err_max - ngals_plot, $
                              /hibar, psym=psym_use, $
                              color=color, errcolor=color, _STRICT_EXTRA=e
              ENDELSE
          ENDIF ELSE BEGIN
              IF n_elements(euclid) GT 0 THEN BEGIN
                  <span class="comments">;; Without error bars</span>
                  oplot, bincentres, $
                         ngals_plot / 10 ^ (0.6 * bincentres) / euclid, $
                         psym=psym_use, $
                         color=color, _STRICT_EXTRA=e                  
              ENDIF ELSE BEGIN
                  <span class="comments">;; Without error bars</span>
                  oplot, bincentres, ngals_plot, $
                         psym=psym_use, $
                         color=color, _STRICT_EXTRA=e
              ENDELSE
          ENDELSE
      ENDIF

      <span class="comments">;; Add legend?</span>
      ajs_number_counts_plot_legend, $
         legend_text=legend_text, legend_pos=legend_pos, $
         arg_legend_pos=arg_present(legend_pos), $
<span class="comments">;;                           xrange=xrange, yrange=yrange, logflux=logflux, $</span>
<span class="comments">;;                           plot_logn=plot_logn, $</span>
         color=color, psym=psym_use, $
         _EXTRA=e
  ENDIF

  ajs_plot_stop, plotfile=plotfile, show_plot=show_plot, open=open, close=close
      
  RETURN
END
</code>
    </div>
  </body>
</html>