<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.0 on Fri Jan 16 23:50:50 2009 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>ajs_bbd_plot.pro (Anthony Smith's IDL routines)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="ajs_bbd_plot.pro (Anthony Smith's IDL routines)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; docformat = 'rst'</span>

<span class="comments">;+</span>
<span class="comments">; This procedure produces a surface/contour plot of the</span>
<span class="comments">; bivariate brightness distribution (number of galaxies per unit</span>
<span class="comments">; volume as a function of absolute magnitude and surface brightness)</span>
<span class="comments">;-</span>

<span class="comments">;+</span>
<span class="comments">; Main procedure.</span>
<span class="comments">;</span>
<span class="comments">; :Params:</span>
<span class="comments">;    bincentres1 : in, out, optional</span>
<span class="comments">;       Centres of absolute magnitude bins (x-axis). Optional</span>
<span class="comments">;       if calculating BBD from m1, m2 and weights</span>
<span class="comments">;    bincentres2 : in, out, optional</span>
<span class="comments">;       Centres of surface brightness bins (y-axis). Optional</span>
<span class="comments">;       if calculating BBD from m1, m2 and weights</span>
<span class="comments">;    phibbd : in, optional</span>
<span class="comments">;       Value of phi at bincentres1[i], bincentres2[j]. Not required</span>
<span class="comments">;       if calculating BBD from m1, m2 and weights.</span>
<span class="comments">;</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;    phibbderr : in, optional</span>
<span class="comments">;       1-sigma errors in phibbd: converted to 1-sigma errors in</span>
<span class="comments">;       corresponding chi^2, then plotted as dotted/dashed contours. Set</span>
<span class="comments">;       /phibbderr, or supply variable, with m1, m2 to calculate then</span>
<span class="comments">;       plot errors</span>
<span class="comments">;    error_contours : in, optional</span>
<span class="comments">;       Set /error_contours to force plotting of errors (switches off</span>
<span class="comments">;       if Choloniewski function is plot)</span>
<span class="comments">;    ct : in, optional</span>
<span class="comments">;       Color table to use (default 1)</span>
<span class="comments">;    plotfile : in, optional</span>
<span class="comments">;       Destination for output EPS plot</span>
<span class="comments">;    show_plot : in, optional</span>
<span class="comments">;       Open EPS plot for display</span>
<span class="comments">;    band : in, optional, type=string</span>
<span class="comments">;       Name of waveband (for axis titles)</span>
<span class="comments">;    title : in, optional</span>
<span class="comments">;    xtitle : in, optional</span>
<span class="comments">;    ytitle : in, optional</span>
<span class="comments">;    colorbartitle : in, optional</span>
<span class="comments">;    min_log_phi : in, optional</span>
<span class="comments">;    max_log_phi : in, optional</span>
<span class="comments">;    choloniewski : in, out, optional</span>
<span class="comments">;       If a Choloniewski fit is performed, error contours will not be</span>
<span class="comments">;       displayed, unless /error_contours is set</span>
<span class="comments">;    chol_xrange : in, optional, type=fltarr(2)</span>
<span class="comments">;       xrange for Choloniewski fit (ajs_choloniewski_fit)</span>
<span class="comments">;    chol_yrange : in, optional, type=fltarr(2)</span>
<span class="comments">;       yrange for Choloniewski fit (ajs_choloniewski_fit)</span>
<span class="comments">;    cov_mat : out, optional</span>
<span class="comments">;       Covariance matrix of Choloniewski fit</span>
<span class="comments">;    m1 : in, optional</span>
<span class="comments">;       Values of first magnitude for each galaxy (if phibbd empty)</span>
<span class="comments">;    m2 : in, optional</span>
<span class="comments">;       Values of second magnitude for each galaxy (if phibbd empty)</span>
<span class="comments">;    weights : in, optional</span>
<span class="comments">;       Weight (e.g., 1/Vmax) for each galaxy (if phibbd empty). See</span>
<span class="comments">;       ajs_vmax_bbd for more details.</span>
<span class="comments">;    jackknife : in, optional</span>
<span class="comments">;       Integer for each galaxy giving the jackknife region in which</span>
<span class="comments">;       the galaxy lies</span>
<span class="comments">;    xrange : in, optional</span>
<span class="comments">;       Range of plot</span>
<span class="comments">;    yrange : in, optional</span>
<span class="comments">;       Range of plot</span>
<span class="comments">;    nbins1 : in, optional</span>
<span class="comments">;       Number of bins for BBD calculation from m2 and weights</span>
<span class="comments">;    nbins2 : in, optional</span>
<span class="comments">;       Number of bins for BBD calculation from m2 and weights</span>
<span class="comments">;    vars : in, optional</span>
<span class="comments">;       See ajs_choloniewski_fit</span>
<span class="comments">;    zmin : in, optional</span>
<span class="comments">;       See ajs_choloniewski_fit</span>
<span class="comments">;    zmax : in, optional</span>
<span class="comments">;       See ajs_choloniewski_fit</span>
<span class="comments">;    area : in, optional</span>
<span class="comments">;       See ajs_choloniewski_fit</span>
<span class="comments">;    q0 : in, optional</span>
<span class="comments">;       See ajs_choloniewski_fit</span>
<span class="comments">;    q1 : in, optional</span>
<span class="comments">;       See ajs_choloniewski_fit</span>
<span class="comments">; :Examples:</span>
<span class="comments">;    ajs_bbd_plot, bincentres1, bincentres2, phibbd</span>
<span class="comments">;</span>
<span class="comments">;    ajs_bbd_plot, m1=abs_mag, m2=surface_brightness, weights=weights</span>
<span class="comments">; :History:</span>
<span class="comments">;    11 Jan 2008 Created, Anthony Smith</span>
<span class="comments">;</span>
<span class="comments">;    18 Mar 2008 Added jackknife</span>
<span class="comments">;</span>
<span class="comments">;    25 Mar 2008 Added keywords for Choloniewski fit</span>
<span class="comments">;</span>
<span class="comments">;    7 Apr 2008 Choloniewski included in ajs_vmax_bbd call</span>
<span class="comments">;-</span>
PRO ajs_bbd_plot, bincentres1, bincentres2, phibbd, phibbderr=phibbderr, $
                  error_contours=error_contours, $
                  plotfile=plotfile, ct=ct, show_plot=show_plot, $
                  band=band, title=title, xtitle=xtitle, ytitle=ytitle, $
                  colorbartitle=colorbartitle, min_log_phi=min_log_phi, $
                  max_log_phi=max_log_phi, $
                  choloniewski=choloniewski, chol_xrange=chol_xrange, $
                  chol_yrange=chol_yrange, cov_mat=cov_mat, $
                  m1=m1, m2=m2, weights=weights, jackknife=jackknife, $
                  xrange=xrange, yrange=yrange, $
                  nbins1=nbins1, nbins2=nbins2, $
                  vars=vars, zmin=zmin, zmax=zmax, area=area, q0=q0, q1=q1
  compile_opt idl2

  debug = ajs_debug()
  IF debug GE 1 THEN BEGIN
      message, 'BBD plot', /inf
      message, ajs_kw_string(zmin=zmin, zmax=zmax, area=area, q0=q0, q1=q1), $
               /inf
  ENDIF

  <span class="comments">;; Deal with parameters & keywords</span>
  IF n_elements(error_contours) EQ 0 THEN $ 
     <span class="comments">;; Show error contours if phibbderr set but not if Choloniewski fit</span>
     error_contours = (n_elements(phibbderr) GT 0 OR arg_present(phibbderr)) $
                      AND ~ ((n_elements(choloniewski) LE 1 $
                              AND (arg_present(choloniewski) $
                                   OR keyword_set(choloniewski))))
  IF n_elements(ct) EQ 0 THEN $
     ct = 1                     <span class="comments">; Colour table</span>
  IF n_elements(band) EQ 0 THEN $
     band = ''
  IF n_elements(xtitle) EQ 0 THEN $ 
     xtitle = 'M!D' + band + '!N-5log h'
  IF n_elements(ytitle) EQ 0 THEN $ 
     ytitle = '!4l!3!D' + band + '!N'
  IF n_elements(colorbartitle) EQ 0 THEN $  
     colorbartitle = 'log !4u!3'

  <span class="comments">;; Calculate BBD from raw data?</span>
  IF n_elements(phibbd) EQ 0 THEN BEGIN
      IF n_elements(m1) EQ n_elements(m2) THEN BEGIN
          <span class="comments">;; Include Choloniewski fit?</span>
          IF (n_elements(choloniewski) LE 1 $
              AND (arg_present(choloniewski) $
                   OR keyword_set(choloniewski))) THEN BEGIN
              IF debug GE 2 THEN message, '1/Vmax with Choloniewski', /inf
              phibbd = $
                 ajs_vmax_bbd(m1, m2, weights, bincentres1=bincentres1, $
                              bincentres2=bincentres2, err_phi=phibbderr, $
                              xrange=xrange, yrange=yrange, $
                              nbins1=nbins1, nbins2=nbins2, $
                              jackknife=jackknife, choloniewski=choloniewski, $
                              chol_xrange=chol_xrange, $
                              chol_yrange=chol_yrange, vars=vars, $
                              zmin=zmin, zmax=zmax, area=area, $
                              q0=q0, q1=q1, cov_mat=cov_mat)
          ENDIF ELSE BEGIN
              IF debug GE 2 THEN message, '1/Vmax (no Choloniewski)', /inf
              phibbd = $
                 ajs_vmax_bbd(m1, m2, weights, bincentres1=bincentres1, $
                              bincentres2=bincentres2, err_phi=phibbderr, $
                              xrange=xrange, yrange=yrange, $
                              nbins1=nbins1, nbins2=nbins2, $
                              jackknife=jackknife)
          ENDELSE
      ENDIF
  ENDIF

  IF n_elements(min_log_phi) EQ 0 THEN BEGIN
      min_log_phi = floor(alog10(min(phibbd[where(phibbd GT 0)])))
      min_log_phi = max([min_log_phi, -10])
  ENDIF
  IF n_elements(max_log_phi) EQ 0 THEN $
     max_log_phi = ceil(alog10(max(phibbd[where(finite(phibbd))])))
  levels_phi_range_log = max_log_phi - min_log_phi
  
  <span class="comments">;; Contour levels (NB if the first is '' it substitutes numbers for all)</span>
  lev_all = [1e-10, 10.^(-9.5), 1e-9, 10.^(-8.5), 1e-8, 10.^(-7.5), 1e-7, $
             10.^(-6.5), $
             1e-6, 10.^(-5.5), 1e-5, 10.^(-4.5), 1e-4, 10.^(-3.5), 1e-3, $
             10.^(-2.5), 1e-2, $
             10.^(-1.5), 1e-1, 10.^(-0.5), 1, 10.^(0.5), 1e1, 10.^(1.5), $
             1e2, 10.^(2.5), $
             1e3, 10.^(3.5), 1e4, 10.^(4.5), 1e5, 10.^(5.5), 1e6, 10.^(6.5), $
             1e7, 10.^(7.5), $
             1e8, 10.^(8.5), 1e9]
  c_annot_all = (['1e-10', '', '1e-9', '', '1e-8', '', '1e-7', '', '1e-6', $
                   '', '1e-5', '', '1e-4', '', '1e-3', '', '1e-2', '', $
                   '1e-1', '', '1e0', '', '1e1', '', '1e2', '', '1e3', '', $
                   '1e4', '', '1e5', '', '1e6', '', '1e7', '', '1e8', '', $
                   '1e9'])
  c_annotation = c_annot_all[ $
                 where(lev_all GE 10. ^ min_log_phi $
                       AND lev_all LE 10. ^ max_log_phi)]
  levels = lev_all[where(lev_all GE  10. ^ min_log_phi $
                         AND lev_all LE 10. ^ max_log_phi)]

  <span class="comments">;; Continuous contour levels</span>
  levels_phi = 10 ^ (findgen(256) / 255 * levels_phi_range_log $
                     + min_log_phi)
  c_colors_phi = indgen(256)

  <span class="comments">;; Ranges, backwards (faint -> bright)</span>
  binsize1 = (max(bincentres1) - min(bincentres1)) / n_elements(bincentres1) 
  binsize2 = (max(bincentres2) - min(bincentres2)) / n_elements(bincentres2) 
  IF n_elements(xrange) EQ 0 THEN $ 
     xrange = [max(bincentres1) + binsize1 / 2., $
               min(bincentres1) - binsize1 / 2.]
  IF n_elements(yrange) EQ 0 THEN $
     yrange = [max(bincentres2) + binsize2 / 2., $
               min(bincentres2) - binsize2 / 2.]

  <span class="comments">;; Plot</span>
  eps = keyword_set(open) OR keyword_set(show_plot)
  ajs_plot_start, plotfile=plotfile, eps=eps, $
                  close=close, bits=8 <span class="comments">; 8 bits per colour = 24 bits</span>

  <span class="comments">;; Axes</span>
  set_basic_colours
  plot, [0], [0], /nodata, xrange=xrange, yrange=yrange, $
        xtitle=xtitle, ytitle=ytitle, /xstyle, /ystyle, $
        position=[0.1275, 0.11, 0.862, 0.945], title=title

  <span class="comments">;; Shaded (filled) contours</span>
  loadct, ct, /silent
  <span class="comments">;; Smooth contours</span>
<span class="comments">;;   xout = findgen(201) / 200 * (max(bincentres1) - min(bincentres1)) $</span>
<span class="comments">;;          + min(bincentres1)</span>
<span class="comments">;;   yout = findgen(201) / 200 * (max(bincentres2) - min(bincentres2)) $</span>
<span class="comments">;;          + min(bincentres2)</span>
<span class="comments">;;   contour, $</span>
<span class="comments">;;      min_curve_surf(phibbd, xvalues=bincentres1, yvalues=bincentres2, $</span>
<span class="comments">;;                     xout=xout, yout=yout), $</span>
<span class="comments">;;      xout, yout, /overplot, c_charsize=!P.charsize, color=1, $</span>
<span class="comments">;;      levels=levels_phi, $</span>
<span class="comments">;;      c_color=c_colors_phi, /fill, position=[0.1275, 0.11, 0.862, 0.945]</span>
  contour, $
     phibbd, $
     bincentres1, bincentres2, /overplot, $<span class="comments">;, color=1, $</span>
     levels=levels_phi, $
     c_color=c_colors_phi, /fill, position=[0.1275, 0.11, 0.862, 0.945]

  <span class="comments">;; Colour bar</span>
  colorbar, ncolors=n_elements(c_colors_phi), $
            minrange=alog10(min(levels_phi)), $
            maxrange=alog10(max(levels_phi)), $
            /vertical, position=[0.912, 0.11, 0.962, 0.945], $
            title=colorbartitle, AnnotateColor='black', Color=255, $
            divisions=levels_phi_range_log

  <span class="comments">;; Labelled contours</span>
  set_basic_colours
  <span class="comments">;; Smooth contours</span>
<span class="comments">;;   xout = findgen(201) / 200 * (max(bincentres1) - min(bincentres1)) $</span>
<span class="comments">;;          + min(bincentres1)</span>
<span class="comments">;;   yout = findgen(201) / 200 * (max(bincentres2) - min(bincentres2)) $</span>
<span class="comments">;;          + min(bincentres2)</span>
<span class="comments">;;   contour, $</span>
<span class="comments">;;      min_curve_surf(phibbd, xvalues=bincentres1, yvalues=bincentres2, $</span>
<span class="comments">;;                     xout=xout, yout=yout), $</span>
<span class="comments">;;      xout, yout, /overplot, $</span>
<span class="comments">;;      levels=levels, $</span>
<span class="comments">;;      c_annotation=c_annotation, c_charsize=!P.charsize, $</span>
<span class="comments">;;      position=[0.1275, 0.11, 0.862, 0.945], c_charthick=2, $</span>
<span class="comments">;;      thick=3</span>
  contour, $
     phibbd, $
     bincentres1, bincentres2, /overplot, $
     levels=levels, $
     c_annotation=c_annotation[*], $
     position=[0.1275, 0.11, 0.862, 0.945]
     thick=1.5 * (1 > !P.thick)

  <span class="comments">;; Choloniewski fit (only if parameters not supplied, and if not done above)</span>
  IF (n_elements(choloniewski) LE 1 $
      AND (arg_present(choloniewski) $
           OR keyword_set(choloniewski))) THEN BEGIN
      <span class="comments">;; Calculate best-fitting Choloniewski function</span>
      <span class="comments">;; NB phibbderr modified by this</span>
      choloniewski = ajs_choloniewski_fit(bincentres1, bincentres2, $
                                          phibbd, phibbderr, $
                                          xrange=chol_xrange, $
                                          yrange=chol_yrange, vars=vars, $
                                          zmin=zmin, zmax=zmax, area=area, $
                                          q0=q0, q1=q1, cov_mat=cov_mat)
  ENDIF

  IF error_contours THEN BEGIN
      IF debug GE 1 THEN message, 'Doing error contours', /inf
      <span class="comments">;; Convert input errors to 1-sigma errors in chi^2, by scaling</span>
      <span class="comments">;; so that chi^2 in each bin is equal, and taking into account</span>
      <span class="comments">;; the number of degrees of freedom</span>
      IF min(phibbderr) LE 0 THEN $
         message, 'Some bins have zero error: bit unrealistic?', /inf
      k = n_elements(phibbderr)
      chi2_bin = sqrt(2. / k)
      phibbderr_chi2 = sqrt(chi2_bin) * phibbderr
      contour, $ 
         phibbd + phibbderr_chi2, $
         bincentres1, bincentres2, /overplot, $
         levels=levels[1:*:2], c_linestyle=1, $
         position=[0.1275, 0.11, 0.862, 0.945], thick=1.5 * (1 > !P.thick)
      contour, $
         phibbd - phibbderr_chi2, $
         bincentres1, bincentres2, /overplot, $
         levels=levels[1:*:2], c_linestyle=2, $
         position=[0.1275, 0.11, 0.862, 0.945], thick=1.5 * (1 > !P.thick)

  ENDIF

  IF n_elements(choloniewski) GT 1 THEN BEGIN
      <span class="comments">;; Plot Choloniewski fit contours</span>
      chol_m1 = ajs_linspace(min(xrange), max(xrange), 101)
      chol_m2 = ajs_linspace(min(yrange), max(yrange), 101)
      chol_bbd = ajs_choloniewski(chol_m1, chol_m2, $
                                  choloniewski)
      contour, $
         chol_bbd, $
         chol_m1, chol_m2, /overplot, $
         levels=lev_all, $
         c_annotation=c_annot_all, $
         position=[0.1275, 0.11, 0.862, 0.945], $
         thick=1.5 * (1 > !P.thick), c_linestyle=1, c_colors=1
      <span class="comments">;; Display Choloniewski parameters on the plot</span>
<span class="comments">;;       xyouts, xrange[0] + (xrange[1] - xrange[0]) / 25., $</span>
<span class="comments">;;               yrange[1] - (yrange[1] - yrange[0]) / 15., $</span>
<span class="comments">;;               string(choloniewski[0], format='(F0.2)') + ', ' $</span>
<span class="comments">;;               + string(choloniewski[1], format='(F0.2)') + ', ' $</span>
<span class="comments">;;               + string(choloniewski[2], format='(e0.1)') + ', ' $</span>
<span class="comments">;;               + string(choloniewski[3], format='(F0.2)') + ', ' $</span>
<span class="comments">;;               + string(choloniewski[4], format='(F0.2)') + ', ' $</span>
<span class="comments">;;               + string(choloniewski[5], format='(F0.2)')</span>
<span class="comments">;;       xyouts, xrange[0] + (xrange[1] - xrange[0]) / 25., $</span>
<span class="comments">;;               yrange[1] - 1.5 * (yrange[1] - yrange[0]) / 15., $</span>
<span class="comments">;;               string(choloniewski[3], format='(F0.2)') + ', ' $</span>
<span class="comments">;;               + string(choloniewski[4], format='(F0.2)') + ', ' $</span>
<span class="comments">;;               + string(choloniewski[5], format='(F0.2)')</span>
  ENDIF  

  ajs_plot_stop, plotfile=plotfile, show_plot=show_plot, open=open, close=close

END

<span class="comments">;+</span>
<span class="comments">; Test ajs_bbd_plot</span>
<span class="comments">;-</span>
PRO ajs_bbd_plot_test, show_plot, _REF_EXTRA=e
  compile_opt idl2

  IF n_elements(show_plot) EQ 0 THEN $
     show_plot = 0

<span class="comments">;;   default_dir = '/Users/anthonys/Documents/UKIDSS/DR3plus/'</span>
<span class="comments">;;   ajs_bbd_read, default_dir + '/multiLum/default/multiLum/bbd_vmax.dat', $</span>
<span class="comments">;;                 bincentres1, bincentres2, phibbd, phibbderr</span>

  bincentres1 = ajs_linspace(-16, -26, 24)
  bincentres2 = ajs_linspace(21, 14, 24)
  phibbd = ajs_choloniewski(bincentres1, bincentres2, $
                            [-23, -1, 2e-2, 17, 0.5, 0.3])
  phibbderr = sqrt(phibbd)

  <span class="comments">;; Choloniewski fit</span>
  <span class="comments">;; Cannot have zero errors: where err is zero, substitue 1e-4 (fudge)</span>
  chol_phierr = phibbderr
  IF min(phibbderr) LE 0 THEN $
     chol_phierr[where(phibbderr EQ 0)] = 1e-4
  
  <span class="comments">;; Fit Choloniewski function</span>
<span class="comments">;;   chol_params = ajs_choloniewski_fit(bincentres1, bincentres2, $</span>
<span class="comments">;;                                      phibbd, chol_phierr, $</span>
<span class="comments">;;                                      txtfile=txtfile)</span>

  ajs_bbd_plot, bincentres1, bincentres2, phibbd, phibbderr=chol_phierr, $
                show_plot=show_plot, $
                _STRICT_EXTRA=e, choloniewski=choloniewski

  print, choloniewski

  IF show_plot THEN $
     wait, 2 $
  ELSE $
     window, !d.window + 1
  
  ajs_bbd_plot, bincentres1, bincentres2, phibbd, $
                show_plot=show_plot, $
                _STRICT_EXTRA=e, choloniewski=choloniewski


END
</code>
    </div>
  </body>
</html>