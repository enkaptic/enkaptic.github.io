<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
 "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">

<!-- Generated by IDLdoc 3.0 on Fri Jan 16 23:50:52 2009 -->

<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
  <head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8"/>
    <title>ajs_double_schechter_fit.pro (Anthony Smith's IDL routines)</title>

    
    <link rel="stylesheet" type="text/css" media="all"
          href="../idldoc-resources/main.css" />
    <link rel="stylesheet" type="text/css" media="print"
          href="../idldoc-resources/main-print.css" />
    

    <script type="text/javascript">
      function setTitle() {
        parent.document.title="ajs_double_schechter_fit.pro (Anthony Smith's IDL routines)";
      }
    </script>
  </head>

  <body onload="setTitle();" id="root">
    <div class="content">
      <code class="source"><span class="comments">; docformat = 'rst'</span>
<span class="comments">;+</span>
<span class="comments">; This function finds the best-fitting double Schechter function for a</span>
<span class="comments">; given array of absolute magnitudes, phi and phi_err (alpha_2 > alpha_1)</span>
<span class="comments">;-</span>

<span class="comments">;+</span>
<span class="comments">; This function finds the best-fitting double Schechter function for a</span>
<span class="comments">; given array of absolute magnitudes, phi and phi_err (alpha_2 > alpha_1)</span>
<span class="comments">; :Returns:</span>
<span class="comments">;    [mstar, alpha_1, phistar_1, alpha_2, phistar_2]</span>
<span class="comments">; :Params:</span>
<span class="comments">;    absmag : in, required</span>
<span class="comments">;    phi : in, required</span>
<span class="comments">;    phierr : in, required</span>
<span class="comments">;    params_init : in, optional</span>
<span class="comments">;       Initial guess for [mstar, alpha_1, phistar_1, alpha_2, phistar_2]</span>
<span class="comments">;       (or default)</span>
<span class="comments">; :Keywords:</span>
<span class="comments">;    txtfile : in, optional</span>
<span class="comments">;       Output text file for Schechter parameters</span>
<span class="comments">;    loglum : in, optional</span>
<span class="comments">;       Set to input log10 luminosity instead of absolute magnitudes</span>
<span class="comments">;    cov_mat : out, optional</span>
<span class="comments">;       Covariance matrix</span>
<span class="comments">;    range : in, optional, type=fltarr(2)</span>
<span class="comments">;       [min, max] absmag for fitting</span>
<span class="comments">;    lum_dens : out, optional</span>
<span class="comments">;       Luminosity density.  Return</span>
<span class="comments">;       value is j = ... x 10^(0.4 M_sun) h L_sun Mpc^-3</span>
<span class="comments">;    err_lum_dens : out, optional</span>
<span class="comments">;       Error in luminosity density</span>
<span class="comments">;    _REF_EXTRA : in, optional</span>
<span class="comments">;       Extra keywords for mpfitfun</span>
<span class="comments">; :Uses:</span>
<span class="comments">;    mpfit (idlutils)</span>
<span class="comments">;</span>
<span class="comments">;    ajs_schechter</span>
<span class="comments">; :History:</span>
<span class="comments">;    5 Sep 2007 Created, Anthony Smith</span>
<span class="comments">;</span>
<span class="comments">;    17 Jan 2008 Added loglum keyword and text file output, AJS</span>
<span class="comments">;</span>
<span class="comments">;    4 Apr 2008 Added range keyword</span>
<span class="comments">;</span>
<span class="comments">;    15 Apr 2008 Added lum_dens</span>
<span class="comments">;</span>
<span class="comments">;    17 Jul 2008 lum_dens now gives correct result for loglum</span>
<span class="comments">;-</span>
FUNCTION ajs_double_schechter_fit, $
   absmag, phi, phierr, params_init, txtfile=txtfile, $
   loglum=loglum, cov_mat=cov_mat, range=range, $
   lum_dens=lum_dens, err_lum_dens=err_lum_dens, $
   _REF_EXTRA=e
  compile_opt idl2
  debug = ajs_debug()
  IF debug GE 1 THEN message, 'Finding double Schechter fit', /inf

  IF n_elements(params_init) EQ 0 THEN $
     params_init=[median(absmag), -0.5, 0.005, -1.5, 0.005]

  IF NOT keyword_set(loglum) THEN $
     loglum = 0

  IF n_elements(range) GT 1 THEN BEGIN
      IF range[0] GT range[1] THEN $
         range = reverse(range) <span class="comments">; [min, max] rather than [max, min]</span>
      fit = where(phierr GT 0 AND absmag GE range[0] AND absmag LE range[1], $
                  ncomplement=n_no_error_or_outside_range)
      res = where(absmag GE range[0] AND absmag LE range[1], $
                  ncomplement=n_outside_range)
      n_no_error = n_no_error_or_outside_range - n_outside_range
  ENDIF ELSE $
     fit = where(phierr GT 0, ncomplement=n_no_error)

  <span class="comments">;; Undefined errors?</span>
  IF n_no_error GT 0 THEN $
     message, 'Ignoring bins with zero/undefined error.', /inf

  <span class="comments">;; Perform fit</span>
  params=mpfitfun('ajs_double_schechter', $
                  absmag[fit], $
                  phi[fit], $
                  phierr[fit], $
                  params_init, /quiet, covar=cov_mat, perror=perror, $
                  functargs=create_struct('loglum', loglum), _STRICT_EXTRA=e)

  <span class="comments">;; Require alpha_2 > alpha_1</span>
  IF params[1] GT params[3] THEN $
     params = [params[0], params[3], params[4], params[1], params[2]]

  <span class="comments">;; Text file</span>
  IF n_elements(txtfile) GT 0 THEN BEGIN
      openw, unit, txtfile, /get_lun
      printf, unit, params
      free_lun, unit
  ENDIF

  <span class="comments">;; Luminosity density</span>
  IF keyword_set(loglum) THEN $
     lum_dens = $
     params[2] * 10. ^ (params[0]) * gamma(params[1] + 2) $
     + params[4] * 10. ^ (params[0]) * gamma(params[3] + 2) $
  ELSE $
     lum_dens = $
     params[2] * 10. ^ ((- params[0]) / 2.5) * gamma(params[1] + 2) $
     + params[4] * 10. ^ ((- params[0]) / 2.5) * gamma(params[3] + 2)
  <span class="comments">;; Variance: sigma_g^2 = sum_i,j ( dg/dx_i dg/dx_j cov(x_i, x_j) )</span>
  <span class="comments">;; (partial derivatives)</span>
  IF arg_present(err_lum_dens) THEN BEGIN
      IF keyword_set(loglum) THEN BEGIN 
          partial_derivs = [alog(10) * lum_dens, $
                            ajs_digamma(params[1] + 2) * lum_dens, $
                            lum_dens / params[2], $
                            ajs_digamma(params[3] + 2) * lum_dens, $
                            lum_dens / params[4]]
          var_lum_dens = partial_derivs ## cov_mat ## (1 # partial_derivs)
          err_lum_dens = sqrt(var_lum_dens)
      ENDIF ELSE BEGIN
          partial_derivs = [- alog(10) / 2.5 * lum_dens, $
                            ajs_digamma(params[1] + 2) * lum_dens, $
                            lum_dens / params[2], $
                            ajs_digamma(params[3] + 2) * lum_dens, $
                            lum_dens / params[4]]
          var_lum_dens = partial_derivs ## cov_mat ## (1 # partial_derivs)
          err_lum_dens = sqrt(var_lum_dens)
      ENDELSE
  ENDIF

  RETURN, params
END


<span class="comments">;+</span>
<span class="comments">; Test ajs_schechter_fit</span>
<span class="comments">;-</span>
PRO ajs_double_schechter_fit_test

  params_true = [-25, -1.35, 0.00567, -0.45, 0.02123]
  m = ajs_linspace(-18, -26, 51)
  print,'Starting with', params_true
  phi = ajs_double_schechter(m, params_true)
  phierr = phi / 10
  ajs_lf_plot, bincentres=m, phi=phi, /ylog

  <span class="comments">;; Fit</span>
  params = ajs_double_schechter_fit(m, phi, phierr, cov_mat=cov_mat)
  print,'Fitted parameters:', params
  phifit = ajs_double_schechter(m, params)
  ajs_lf_plot, bincentres=m, phi=phifit, color=1, linestyle=5, /overplot

  print, 'Covariance matrix (double):'
  print, cov_mat, format='(5F12.8)'
  print, 'Correlation matrix (double):'
  print, ajs_corr_mat(cov_mat), format='(5F8.4)'

  ajs_pause

  params_true = [10, -1.35, 0.00567, -0.45, 0.02123]
  m = ajs_linspace(8, 11, 51)
  print,'Starting with', params_true
  phi = ajs_double_schechter(m, params_true, /loglum)
  phierr = phi / 10
  ajs_lf_plot, bincentres=m, phi=phi, /ylog, /loglum

  <span class="comments">;; Fit</span>
  params = ajs_double_schechter_fit(m, phi, phierr, cov_mat=cov_mat, /loglum)
  print,'Fitted parameters:', params
  phifit = ajs_double_schechter(m, params, /loglum)
  ajs_lf_plot, bincentres=m, phi=phifit, color=1, linestyle=5, /overplot, $
               /loglum

  print, 'Covariance matrix (double):'
  print, cov_mat, format='(5F12.8)'
  print, 'Correlation matrix (double):'
  print, ajs_corr_mat(cov_mat), format='(5F8.4)'



END
</code>
    </div>
  </body>
</html>